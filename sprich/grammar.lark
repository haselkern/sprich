start: state+

state.1: "[" identifier "]" _NEWLINE (statement|_NEWLINE)*

?statement: message _NEWLINE
          | function _NEWLINE
          | option _NEWLINE
          | instant _NEWLINE
          | _COMMENT _NEWLINE

message: STRING

option: ">" _spaces STRING _spaces ["[" identifier "]" _spaces [condition]]
condition: STRING

function: "\\" identifier parameters
parameters: (_spaces parameter)*
parameter: SIGNED_NUMBER | NUMBER | STRING | "true" | "false"

instant: "->" _spaces "[" identifier "]" [_spaces condition]

_COMMENT: "#" /.+/

identifier: IDENTIFIER
IDENTIFIER: (LETTER|UNDERSCORE|DIGIT)+
          | PLUS

_spaces: [_SPACES]
_SPACES: (" "|"\t")+
PLUS: "+"
LETTER: "a".."z"
      | "A".."Z"
UNDERSCORE: "_"
_NEWLINE: "\n"

%import common.ESCAPED_STRING -> STRING
%import common.DIGIT
%import common.SIGNED_NUMBER
%import common.NUMBER
